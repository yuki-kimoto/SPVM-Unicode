# Copyright (c) 2023 Yuki Kimoto
# MIT License

class Unicode : precompile {
  version "0.051";
  
  use IntList;
  
  native static method uchar : int ($string : string, $offset_ref : int*);
  native static method uchar_to_utf8 : string ($uchar : int);
  
  enum {
    ERROR_INVALID_UTF8 = -2,
  }
  
  static method is_unicode_scalar_value : int ($code_point: int) {
    my $is_unicode_scalar_value = 0;
    # The range of Unicde code points
    if ($code_point >= 0 && $code_point <= 0x10FFFF) {
      # Not surrogate code points
      unless ($code_point >= 0xD800 && $code_point <= 0xDFFF) {
        $is_unicode_scalar_value = 1;
      }
    }
    
    return $is_unicode_scalar_value;
  }
 
  static method utf8_to_utf16 : short[] ($string : string) {
    my $pos = 0;
    my $uchars_list = IntList->new_len(0);
    while ((my $uchar = &uchar($string, \$pos)) >= 0) {
      $uchars_list->push($uchar);
    }
    
    unless ($pos >= length $string) {
      die "Can't convert part of string to utf16";
    }
    
    my $uchars = $uchars_list->to_array;
    
    my $string_ret = &utf32_to_utf16($uchars);
    
    return $string_ret;
  }
  
  static method utf16_to_utf8 : string ($string : short[]) {
    my $uchars = &utf16_to_utf32($string);
    
    my $buffer = StringBuffer->new;
    
    for (my $i = 0; $i < @$uchars; $i++) {
      my $uchar = $uchars->[$i];
      my $utf8_str = &uchar_to_utf8($uchar);
      $buffer->push($utf8_str);
    }
    
    my $string_ret = $buffer->to_string;
    
    return $string_ret;
  }
  
  static method utf32_to_utf16 : short[] ($string : int[]) {
    my $length = 0;
    
    # Culcurate length
    for (my $i = 0; $i < @$string; $i++) {
      my $code_point = $string->[$i];

      if ($code_point < 0 || $code_point > 0x10FFFF) {
        die "Invalid code point in code point string";
      }

      if ($code_point < 0x10000) {
        $length++;
      }
      else {
        $length += 2;
      }
    }
    
    # Convert code point to UTF-16
    my $string_ret = new short[$length];
    my $pos = 0;
    
    for (my $i = 0; $i < @$string; $i++) {
      my $code_point = $string->[$i];

      if ($code_point < 0x10000) {
        $string_ret->[$pos] = (short)$code_point;
        $pos++;
      }
      else {
        $string_ret->[$pos] = (short)(($code_point - 0x10000) / 0x400 + 0xD800);
        $string_ret->[$pos + 1] = (short)(($code_point - 0x10000) % 0x400 + 0xDC00);
        $pos += 2;
      }
    }
    
    return $string_ret;
  }

  static method utf16_to_utf32 : int[] ($string : short[]) {

    my $length = 0;
    
    # Culcurate length
    for (my $i = 0; $i < @$string; $i++) {
      if (&_is_utf16_high_surrogate($string->[$i] & 0xFFFF)) {
        if ($i + 1 == @$string - 1) {
          die "Invalid UTF-16 string";
        }
        else {
          $i++;
          unless (&_is_utf16_low_surrogate($string->[$i] & 0xFFFF)) {
            die "Invalid UTF-16 string";
          }
        }
      }
      elsif (&_is_utf16_low_surrogate($string->[$i] & 0xFFFF)) {
        die "Invalid UTF-16 string";
      }
      $length++;
    }
    
    # Convert UTF-16 to code point
    my $string_ret = new int[$length];
    my $pos = 0;
    for (my $i = 0; $i < @$string; $i++) {
      if (&_is_utf16_high_surrogate($string->[$i] & 0xFFFF)) {
        $string_ret->[$pos] = 0x10000 + (($string->[$i] & 0xFFFF) - 0xD800) * 0x400 + (($string->[$i + 1] & 0xFFFF) - 0xDC00);
        $i++;
      }
      else {
        $string_ret->[$pos] = $string->[$i] & 0xFFFF;
      }
      $pos++;
    }
    
    return $string_ret;
  }

  private static method _is_utf16_high_surrogate : int ($char : int) {
    if ($char >= 0xD800 && $char < 0xDC00) {
      return 1;
    }
    else {
      return 0;
    }
  }

  private static method _is_utf16_low_surrogate : int ($char : int) {
    if ($char >= 0xDC00 && $char < 0xE000) {
      return 1;
    }
    else {
      return 0;
    }
  }
}
